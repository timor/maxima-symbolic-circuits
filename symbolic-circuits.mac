load("eigen")$

reset_circuit(nodes,nbranches):=block([],
    incidence_matrix:zeromatrix(nodes,nbranches),
    voltage_coeffs:zeromatrix(nbranches,nbranches),
    current_coeffs:zeromatrix(nbranches,nbranches),
    voltage_sources:zeromatrix(nbranches,1),
    kill(branches),
    num_nodes:nodes,
    num_branches:nbranches,
    branch_counter:0);

getbranchcreate(snode,tnode):=if numberp(branches[snode,tnode]) then
                                    branches[snode,tnode] elseif
                                numberp(branches[tnode,snode]) then
                                    branches[tnode,snode] else
(branch_counter:branch_counter+1,branches[snode,tnode]:branch_counter,branch_counter)$

impedance(snode,tnode,z):=block([
    branch:getbranchcreate(snode,tnode)],
    incidence_matrix[snode,branch]:1,
    incidence_matrix[tnode,branch]:-1,
    voltage_coeffs[branch,branch]:1,
    current_coeffs[branch,branch]:-z,
    voltage_sources[branch,1]:0
    )$

voltage(snode,tnode,u):=block([
        branch:getbranchcreate(snode,tnode)],
    incidence_matrix[snode,branch]:1,
    incidence_matrix[tnode,branch]:-1,
    voltage_coeffs[branch,branch]:1,
    current_coeffs[branch,branch]:0,
    voltage_sources[branch,1]:u)$

current(snode,tnode,i):=block([
        branch:getbranchcreate(snode,tnode)],
    incidence_matrix[snode,branch]:1,
    incidence_matrix[tnode,branch]:-1,
    voltage_coeffs[branch,branch]:0,
    current_coeffs[branch,branch]:1,
    voltage_sources[branch,1]:i)$

symbols_vector(sym,num,offset):=columnvector(makelist(concat(sym,k),k,1+offset,num+offset));

min_incidence():=submatrix(1,incidence_matrix);

sparse_matrix():=block([
                    row1:zeromatrix(num_nodes-1,num_nodes-1),
                    row2:-transpose(min_incidence()),
                    row3:zeromatrix(num_branches,num_nodes-1)],
    row1:addcol(row1,zeromatrix(num_nodes-1,num_branches),min_incidence()),
    row2:addcol(row2,ident(num_branches),zeromatrix(num_branches,num_branches)),
    row3:addcol(row3,voltage_coeffs,current_coeffs),
    addrow(row1,row2,row3))$

source_vector():= addrow(zeromatrix(num_nodes-1,1),zeromatrix(num_branches,1),voltage_sources);

result_vector():=addrow(symbols_vector('e,num_nodes-1,1),symbols_vector('v,num_branches,0),symbols_vector('i,num_branches,0));

unknowns():=list_matrix_entries(result_vector());

solve_circuit():=map("=",list_matrix_entries(result_vector()),list_matrix_entries(linsolve_by_lu(sparse_matrix(),source_vector())[1]));

sparse_eq():=sparse_matrix() . result_vector() = source_vector();

show_branches():=for i in rest(rest(arrayinfo(branches))) do print(branches[i[1],i[2]], ": ",i);
